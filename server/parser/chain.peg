/*
 * Timesheet syntax parser
 */

start
 = series_list+

series
 = chain
 / request
 / reference

series_list
 = series:series ';'? (space*)? { return series }


chained_function
 = '.' func:function {
  return func
 }

chain
 = '(' space? first:series space? ')' rest:chained_function* {
  first.label = text();
  return {type: "chain", chain: [first].concat(rest)};
 }

function_name
 = (first:[a-zA-Z]+ rest:[a-zA-Z0-9]* ) { return first.join('') + rest.join('') }

function
 = (name:function_name '(' space? args:args? space? ')' space? ';'? space?) {
  return {
    type: 'function',
    function: name,
    arguments: args
  }
 }

args
 = argument+

argument
 = element:arg_type ','? (' '*)? {return element}

arg_type
 = series
 / function
 / number
 / string

reference
 = '@' plot:integer ':' series:integer {
  return {
   type: 'reference',
   plot: plot,
   series: series
  }
 }

// Only 26 columns supported for now
column
 = column:[A-Z]

number
 = number:(integer '.'? integer?) ','? (' '*)? {return parseFloat(number.join(''))}

integer
 = digits:DIGIT+ {return parseInt(digits.join(''))}

query
 = '`' q:[^`]* '`' {return q.join('')}

request_operator
 = (space+ )? operator:function_name (space+)? arguments:request_operator_argument+ (space+)? {
  return {
   operator: operator,
   arguments: arguments
  }
 }

request_operator_argument
 = argument:([a-zA-Z0-9\-_\*.]+ / string) ':'? {
  return argument.join('');
 }

request
 = query:query operators:(request_operator+)?
 {
  var obj = {query: query, type: 'query'}
  if (operators) {
   obj.operators = operators
  }
  return obj;
 }


index
 = (space+)? 'in' (space+)? index_name:index_name
 {
   return index_name
 }

index_name
 = indexName:[A-Za-z0-9\-_.*]+ {
   return index_name.join('')
 }

metric
 = metric:[a-z]+ { return metric.join('')}

field
 = field:[a-zA-Z0-9\-\_\.]+ {return field.join('')}

space
 = [\ \t]+

 /* ----- Strings, borrowed from the PEGJS JSON parser ----- */

string "string"
  = quotation_mark chars:char* quotation_mark { return chars.join(''); }

char
  = unescaped
  / escape
    sequence:(
        '"'
      / "\\"
      / "/"
      / "b" { return "\b"; }
      / "f" { return "\f"; }
      / "n" { return "\n"; }
      / "r" { return "\r"; }
      / "t" { return "\t"; }
      / "u" digits:$(HEXDIG HEXDIG HEXDIG HEXDIG) {
          return String.fromCharCode(parseInt(digits, 16));
        }
    )
    { return sequence; }

escape         = "\\"
quotation_mark = '"'
unescaped      = [\x20-\x21\x23-\x5B\x5D-\u10FFFF]

/* ----- Core ABNF Rules ----- */

/* See RFC 4234, Appendix B (http://tools.ietf.org/html/rfc4627). */
DIGIT  = [0-9]
HEXDIG = [0-9a-f]i